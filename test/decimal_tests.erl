-module(decimal_tests).

-ifdef(TEST).
-include_lib("eunit/include/eunit.hrl").

list_test_() ->
    Tests =
        [
         {"1", {1, 0}},
         {"-1", {-1, 0}}
        ],
    [
     {V, fun() -> R = decimal:from_list(V) end}
     || {V, R} <- Tests
    ].

binary_test_() ->
    PosTests =
        [
         {<<"1">>, {1, 0}},
         {<<"123">>, {123, 0}},
         {<<"1230">>, {123, 1}},
         {<<"1.23">>, {123, -2}},
         {<<"1.2300">>, {123, -2}},
         {<<"1e0">>, {1, 0}},
         {<<"12e1">>, {12, 1}},
         {<<"12300e3">>, {123, 5}},
         {<<"1.2300e0">>, {123, -2}},
         {<<"1.2300e2">>, {123, 0}},
         {<<"1.2300e-2">>, {123, -4}},
         {<<"123.45600e-2">>, {123456, -5}},
         {<<"1e-0">>, {1, 0}},
         {<<"12e-1">>, {12, -1}},
         {<<"12300e-3">>, {123, -1}},
         {<<"1e+0">>, {1, 0}},
         {<<"12e+1">>, {12, 1}},
         {<<"12300e+3">>, {123, 5}}
        ],
    NegTests = [{<<$-, V/binary>>, {-B, E}} || {V, {B, E}} <- PosTests],
    [
     {V, fun() -> R = decimal:normalize(decimal:from_binary(V)) end}
     || {V, R} <- PosTests ++ NegTests
    ].

rounding_test_() ->
    Tests =
        [
         {<<"1">>, 1, floor, <<"1.0">>},
         {<<"1.2">>, 1, floor, <<"1.2">>},
         {<<"1.0">>, 0, floor, <<"1.0">>},
         {<<"1.3">>, 0, floor, <<"1.0">>},
         {<<"1.5">>, 0, floor, <<"1.0">>},
         {<<"1.9">>, 0, floor, <<"1.0">>},
         {<<"-1.0">>, 0, floor, <<"-1.0">>},
         {<<"-1.3">>, 0, floor, <<"-2.0">>},
         {<<"-1.5">>, 0, floor, <<"-2.0">>},
         {<<"-1.9">>, 0, floor, <<"-2.0">>},
         {<<"0.002">>, 2, floor, <<"0.0">>},
         {<<"-0.002">>, 2, floor, <<"-0.01">>},
         {<<"0.00001">>, 0, floor, <<"0.0">>},
         {<<"-0.00001">>, 0, floor, <<"-1.0">>},

         {<<"1">>, 1, ceil, <<"1.0">>},
         {<<"1.2">>, 1, ceil, <<"1.2">>},
         {<<"1.0">>, 0, ceil, <<"1.0">>},
         {<<"1.3">>, 0, ceil, <<"2.0">>},
         {<<"1.5">>, 0, ceil, <<"2.0">>},
         {<<"1.9">>, 0, ceil, <<"2.0">>},
         {<<"-1.0">>, 0, ceil, <<"-1.0">>},
         {<<"-1.3">>, 0, ceil, <<"-1.0">>},
         {<<"-1.5">>, 0, ceil, <<"-1.0">>},
         {<<"-1.9">>, 0, ceil, <<"-1.0">>},
         {<<"0.002">>, 2, ceil, <<"0.01">>},
         {<<"-0.002">>, 2, ceil, <<"0.0">>},
         {<<"0.00001">>, 0, ceil, <<"1.0">>},
         {<<"-0.00001">>, 0, ceil, <<"0.0">>},

         {<<"1">>, 1, half_up, <<"1.0">>},
         {<<"1.2">>, 1, half_up, <<"1.2">>},
         {<<"1.0">>, 0, half_up, <<"1.0">>},
         {<<"1.3">>, 0, half_up, <<"1.0">>},
         {<<"1.5">>, 0, half_up, <<"2.0">>},
         {<<"1.9">>, 0, half_up, <<"2.0">>},
         {<<"-1.0">>, 0, half_up, <<"-1.0">>},
         {<<"-1.3">>, 0, half_up, <<"-1.0">>},
         {<<"-1.5">>, 0, half_up, <<"-2.0">>},
         {<<"-1.9">>, 0, half_up, <<"-2.0">>},
         {<<"0.002">>, 2, half_up, <<"0.0">>},
         {<<"-0.002">>, 2, half_up, <<"0.0">>},
         {<<"0.00001">>, 0, half_up, <<"0.0">>},
         {<<"-0.00001">>, 0, half_up, <<"0.0">>},

         {<<"1">>, 1, half_down, <<"1.0">>},
         {<<"1.2">>, 1, half_down, <<"1.2">>},
         {<<"1.0">>, 0, half_down, <<"1.0">>},
         {<<"1.3">>, 0, half_down, <<"1.0">>},
         {<<"1.5">>, 0, half_down, <<"1.0">>},
         {<<"1.9">>, 0, half_down, <<"2.0">>},
         {<<"-1.0">>, 0, half_down, <<"-1.0">>},
         {<<"-1.3">>, 0, half_down, <<"-1.0">>},
         {<<"-1.5">>, 0, half_down, <<"-1.0">>},
         {<<"-1.9">>, 0, half_down, <<"-2.0">>},
         {<<"0.002">>, 2, half_down, <<"0.0">>},
         {<<"-0.002">>, 2, half_down, <<"0.0">>},
         {<<"0.00001">>, 0, half_down, <<"0.0">>},
         {<<"-0.00001">>, 0, half_down, <<"0.0">>},

         {<<"1">>, 1, down, <<"1.0">>},
         {<<"1.2">>, 1, down, <<"1.2">>},
         {<<"1.0">>, 0, down, <<"1.0">>},
         {<<"1.3">>, 0, down, <<"1.0">>},
         {<<"1.5">>, 0, down, <<"1.0">>},
         {<<"1.9">>, 0, down, <<"1.0">>},
         {<<"-1.0">>, 0, down, <<"-1.0">>},
         {<<"-1.3">>, 0, down, <<"-1.0">>},
         {<<"-1.5">>, 0, down, <<"-1.0">>},
         {<<"-1.9">>, 0, down, <<"-1.0">>},
         {<<"0.002">>, 2, down, <<"0.0">>},
         {<<"-0.002">>, 2, down, <<"0.0">>},
         {<<"0.00001">>, 0, down, <<"0.0">>},
         {<<"-0.00001">>, 0, down, <<"0.0">>}
        ],
    [
     {<<V/binary, " ", (list_to_binary(atom_to_list(R)))/binary>>,
      fun() ->
          D = decimal:from_binary(V),
          D2 = decimal:round(R, D, P),
          Res = decimal:to_binary(D2)
      end}
     || {V, P, R, Res} <- Tests
    ].

sum_test_() ->
    Tests =
        [
         {<<"1">>, <<"2">>, <<"3.0">>},
         {<<"1">>, <<"-2">>, <<"-1.0">>},
         {<<"1">>, <<"0.2">>, <<"1.2">>},
         {<<"1e3">>, <<"1e-3">>, <<"1000.001">>},
         {<<"123.456">>, <<"7e-4">>, <<"123.4567">>},
         {<<"1">>, <<"-1">>, <<"0.0">>}
        ],
    [
     {<<A/binary, $+, B/binary>>,
      fun() ->
          A1 = decimal:from_binary(A),
          B1 = decimal:from_binary(B),
          R = decimal:to_binary(decimal:add(A1, B1))
      end}
     || {A, B, R} <- Tests
    ].

sub_test_() ->
    Tests =
        [
         {<<"1">>, <<"2">>, <<"-1.0">>},
         {<<"1">>, <<"-2">>, <<"3.0">>},
         {<<"1">>, <<"0.2">>, <<"0.8">>},
         {<<"1e3">>, <<"1e-3">>, <<"999.999">>},
         {<<"123.456">>, <<"7e-4">>, <<"123.4553">>},
         {<<"1">>, <<"-1">>, <<"2.0">>}
        ],
    [
     {<<A/binary, $+, B/binary>>,
      fun() ->
          A1 = decimal:from_binary(A),
          B1 = decimal:from_binary(B),
          R = decimal:to_binary(decimal:sub(A1, B1))
      end}
     || {A, B, R} <- Tests
    ].


mult_test_() ->
    Tests =
        [
         {<<"1">>, <<"2">>, <<"2.0">>},
         {<<"1">>, <<"-2">>, <<"-2.0">>},
         {<<"1">>, <<"0.2">>, <<"0.2">>},
         {<<"1e3">>, <<"1e-3">>, <<"1.0">>},
         {<<"1">>, <<"0">>, <<"0.0">>},
         {<<"3e-1">>, <<"3e-1">>, <<"0.09">>}
        ],
    [
     {<<A/binary, $*, B/binary>>,
      fun() ->
          A1 = decimal:from_binary(A),
          B1 = decimal:from_binary(B),
          R = decimal:to_binary(decimal:mul(A1, B1))
      end}
     || {A, B, R} <- Tests
    ].

divide_test_() ->
    Tests =
        [
         {<<"1">>, <<"2">>, <<"0.5">>},
         {<<"1">>, <<"-2">>, <<"-0.5">>},
         {<<"1">>, <<"0.2">>, <<"5.0">>},
         {<<"1e3">>, <<"1e-3">>, <<"1.0e6">>},
         {<<"3e-1">>, <<"3e-1">>, <<"1.0">>},
         {<<"1">>, <<"3">>, <<"0.", (repeate($3, 100))/binary>>},
         {<<"2">>, <<"3">>, <<"0.", (repeate($6, 99))/binary, "7">>},
         {<<"1">>, <<"0">>, {error, badarith}}
        ],
    [
     {<<A/binary, $/, B/binary>>,
      fun() ->
          A1 = decimal:from_binary(A),
          B1 = decimal:from_binary(B),
          R = try
                  decimal:to_binary(decimal:normalize(decimal:round(half_up, decimal:divide(A1, B1, 100), 100)), #{pretty => true})
              catch
                  Err:Res -> {Err,Res}
              end
      end}
     || {A, B, R} <- Tests
    ].

repeate(Ch, N) ->
    << <<Ch>> || _ <- lists:seq(1, N)>>.

neg_test() ->
    ?assertEqual({-1, 0}, decimal:neg({1,0})),
    ?assertEqual({1, 0}, decimal:neg({-1,0})).

abs_test() ->
    ?assertEqual({1, 0}, decimal:abs({1,0})),
    ?assertEqual({1, 0}, decimal:abs({-1,0})).

pretty_print_test_() ->
    Tests =
        [
         {{1, 0}, <<"1.0">>},
         {{1, 1}, <<"10.0">>},
         {{1, 2}, <<"100.0">>},
         {{1, 3}, <<"1000.0">>},
         {{1, 4}, <<"10000.0">>},
         {{1, 5}, <<"100000.0">>},
         {{1, 6}, <<"1.0e6">>},

         {{1, -1}, <<"0.1">>},
         {{1, -2}, <<"0.01">>},
         {{1, -3}, <<"0.001">>},
         {{1, -4}, <<"0.0001">>},
         {{1, -5}, <<"0.00001">>},
         {{1, -6}, <<"1.0e-6">>},

         {{123, 0}, <<"123.0">>},
         {{123, 1}, <<"1230.0">>},
         {{123, 2}, <<"12300.0">>},
         {{123, 3}, <<"123000.0">>},
         {{123, 4}, <<"1.23e6">>},

         {{123, -1}, <<"12.3">>},
         {{123, -2}, <<"1.23">>},
         {{123, -3}, <<"0.123">>},
         {{123, -4}, <<"0.0123">>},
         {{123, -5}, <<"0.00123">>},
         {{123, -6}, <<"0.000123">>},
         {{123, -7}, <<"0.0000123">>},
         {{123, -8}, <<"1.23e-6">>}
        ],
    NegTests = [{{-B, E}, <<$-, R/binary>>} || {{B, E}, R} <- Tests],
    [
     {iolist_to_binary(io_lib:format("~pe~p", [B,E])),
      fun() ->
          R = decimal:to_binary(D, #{pretty => true})
      end}
     || {D={B,E}, R} <- Tests ++ NegTests
    ].

from_number_test_() ->
    Tests =
        [
         {{1,  2}, 100},
         {{1,  1}, 10},
         {{1,  0}, 1.0},
         {{0,  0}, 0.0},
         {{1, -1}, 0.1},
         {{1, -2}, 0.01},
         {{1, -3}, 0.001},
         {{1, -4}, 0.0001},
         {{1, -5}, 0.00001},
         {{1, -6}, 0.000001},

         {{185,  2}, 18500},
         {{185,  1}, 1850},
         {{185,  0}, 185.0},
         {{185, -1}, 18.5},
         {{185, -2}, 1.85},
         {{185, -3}, 0.185},
         {{185, -4}, 0.0185},
         {{185, -5}, 0.00185},
         {{185, -6}, 0.000185}
        ],
    NegTests = [{{-B, E}, -F} || {{B, E}, F} <- Tests],

    [
     {iolist_to_binary(io_lib:format("~p", [F])),
      fun() ->
          D = decimal:normalize(decimal:from_number(F))
      end}
     || {D, F} <- Tests ++ NegTests
    ].

error_badarg_test_() ->
    Tests =
        [
         <<"bad">>,
         <<"-bad">>,
         <<"-1bad">>,
         <<"-1.bad">>,
         <<"-1.0bad">>,
         <<"-1.0ebad">>,
         <<"-1.0e-bad">>,
         <<"-1.0e-1bad">>,
         <<"-1.0e+bad">>
        ],
    [
     {D,
      fun() ->
          ?assertException(error, badarg, decimal:from_binary(D))
      end} || D <- Tests
    ].

cmp_test_() ->
    Tests =
        [
         { {0,0}, {0,0},   0 },
         { {1,0}, {1,0},   0 },
         { {-1,0}, {-1,0}, 0 },
         { {0,0}, {-1,0},  1 },
         { {1,0}, {0,0},   1 },
         { {-1,0}, {0,0}, -1 },
         { {0,0}, {1,0},  -1 },

         { {0, -100}, {0, 100}, 0},
         { {10, 0}, {1, 1}, 0 },
         { {123, -2}, {122, -2}, 1 },
         { {-123, -2}, {-122, -2}, -1 }
        ],
    [
     {iolist_to_binary(
          [
           decimal:to_binary(A),
           case R of
               1 -> $>;
               0 -> $=;
               -1 -> $<
           end,
           decimal:to_binary(B)
          ]),
      fun() ->
          R = decimal:cmp(A, B)
      end} || {A, B, R} <- Tests
    ].

to_binary_test_() ->
    Tests =
        [
         {true, {1, 0}, <<"1.0">>},
         {true, {10, 0}, <<"10.0">>},
         {true, {1, 6}, <<"1.0e6">>},
         {true, {123, -2}, <<"1.23">>},
         {true, {123456, -10}, <<"0.0000123456">>},
         {true, {123456, -11}, <<"1.23456e-6">>},

         {true, {-1, 0}, <<"-1.0">>},
         {true, {-10, 0}, <<"-10.0">>},
         {true, {-1, 6}, <<"-1.0e6">>},
         {true, {-123, -2}, <<"-1.23">>},
         {true, {-123456, -10}, <<"-0.0000123456">>},
         {true, {-123456, -11}, <<"-1.23456e-6">>},

         {false, {1, 0}, <<"1.0">>},
         {false, {10, 0}, <<"10.0">>},
         {false, {1, 6}, <<"1000000.0">>},
         {false, {123, -2}, <<"1.23">>},
         {false, {123456, -10}, <<"0.0000123456">>},
         {false, {123456, -11}, <<"0.00000123456">>},

         {false, {-1, 0}, <<"-1.0">>},
         {false, {-10, 0}, <<"-10.0">>},
         {false, {-1, 6}, <<"-1000000.0">>},
         {false, {-123, -2}, <<"-1.23">>},
         {false, {-123456, -10}, <<"-0.0000123456">>},
         {false, {-123456, -11}, <<"-0.00000123456">>}
        ],
    [
     {iolist_to_binary(
          [
           case P of
               true -> "(pretty) ";
               false -> ""
           end,
           io_lib:format("~p", [D]),
           $=,
           R
          ]),
      fun() ->
          R = decimal:to_binary(D, #{pretty => P})
      end} || {P, D, R} <- Tests
    ].

-endif.
